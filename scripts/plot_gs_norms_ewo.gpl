#!/usr/bin/gnuplot -c

if (ARGC != 1) {
       print "Usage: ", ARG0, " <type:z23|k47>"
       exit status 1;
}

label = ARG1
data_dir = "../data/".label."/"

# Data files
tw   = data_dir.label."_tw.gsn"
opt0 = data_dir.label."_opt0.gsn"
phs0 = data_dir.label."_phs0.gsn"
opt  = data_dir.label."_opt.gsn"
phs  = data_dir.label."_phs.gsn"

# Set the plot grid for all graphs
set border 3
set tics out nomirror
set grid

set ylabel "ln ||bi*||"
set yrange [-1:5]
set xlabel "column index (i)"
set xtics 0,25

set terminal pngcairo size 1000,325


# ---------------------------------------------------------------------------------------------
# Explications 1: 3 graphs at isoFB. Display after/before BKZ for a. TW b. OPT c. PHS

# Multiplot setting and legend
set output data_dir.label."_gsn_expl_wo.png" #.FB_t."_r".r_min."-".r_max.".gsn.png"
# Determining Grid (multiplot), on a 4 graphs max / row basis
nc   = 3 #(nelt < 4)? nc = nelt : 4
nr   = 1 #(nelt) / nc + 1
set multiplot layout nr,nc rowsfirst
set key font ",10" samplen 2 reverse Left
set key box lt -1 lw 0.5 width 1.5


print "    Plot w/o bkz at iso FB for ".label
set size 0.34,1
set title "(a) Twisted-PHS"
#set key at screen 0.1, 0.2 left bottom
plot tw    using 1:2 title "Raw" with line dashtype 2 lc rgb 'red', \
     tw    using 1:3 title "BKZ" with line dashtype 1 lc rgb 'tan1'
unset ylabel
set size 0.33,1
set origin 0.34,0
set title "(b) Optimised-PHS (tw-FB)"
plot opt0  using 1:2 title "Raw" with line dashtype 2 lc rgb 'dark-violet', \
     opt0  using 1:3 title "BKZ" with line dashtype 1 lc rgb 'dark-pink'
set size 0.33,1
set origin 0.67,0
set title "(c) Original PHS (tw-FB)"
plot phs0  using 1:2 title "Raw" with line dashtype 2 lc rgb 'royalblue', \
     phs0  using 1:3 title "BKZ" with line dashtype 1 lc 2


exit status 0;
