#!/usr/bin/gnuplot -c

if (ARGC != 1) {
       print "Usage: ", ARG0, " <type:z23|k47>"
       exit status 1;
}

label = ARG1
data_dir = "../data/".label."/"

# Data files
tw   = data_dir.label."_tw.gsn"
opt0 = data_dir.label."_opt0.gsn"
phs0 = data_dir.label."_phs0.gsn"
opt  = data_dir.label."_opt.gsn"
phs  = data_dir.label."_phs.gsn"

# Set the plot grid for all graphs
set border 3
set tics out nomirror
set grid

set ylabel "ln ||bi*||"
set yrange [-1:5]
set xlabel "column index (i)"
set xtics 0,25

set terminal pngcairo size 1000,325


# ---------------------------------------------------------------------------------------------
# Explications 2: 2 graphs (BKZ only). at isoFB. Display after/before BKZ for a. TW b. OPT c. PHS
# Multiplot setting and legend
set output data_dir.label."_gsn_expl_gh.png" #.FB_t."_r".r_min."-".r_max.".gsn.png"
# Determining Grid (multiplot), on a 4 graphs max / row basis
nc   = 2 #(nelt < 4)? nc = nelt : 4
nr   = 1 #(nelt) / nc + 1
set multiplot layout nr,nc rowsfirst
set key font ",10" samplen 2 reverse Left
set key box lt -1 lw 0.5 width 1


print "    Plot GS log norms after bkz for ".label
set size 0.45,1
set title "(a) At iso factor base (twFB)"
#set key at screen 0.1, 0.2 left bottom
plot tw    using 1:3 title "Tw-PHS " with line dashtype 1 lc rgb 'tan1', \
     opt0  using 1:3 title "Opt-PHS (twFB)" with line dashtype 1 lc rgb 'dark-pink', \
     phs0  using 1:3 title "PHS (twFB)" with line dashtype 1 lc 2

unset ylabel
set size 0.55,1
set origin 0.45,0
set title "(b) At designed factor bases"
set key invert
plot phs   using 1:3 title "PHS" with line dashtype 1 lc 3, \
     opt   using 1:3 title "Opt-PHS" with line dashtype 1 lc rgb 'dark-violet', \
     tw    using 1:3 title "Tw-PHS" with line dashtype 1 lc rgb 'tan1'
# rgb 'royalblue', \
exit status 0;
