#!/usr/bin/gnuplot -c

if (ARGC != 1) {
       print "Usage: ", ARG0, " <type:z23|k47>"
       exit status 1;
}

label = ARG1
data_dir = "../data/".label."/"

# Data files
tw   = data_dir.label."_tw.gsn"
opt0 = data_dir.label."_opt0.gsn"
phs0 = data_dir.label."_phs0.gsn"
opt  = data_dir.label."_opt.gsn"
phs  = data_dir.label."_phs.gsn"

# Set the plot grid for all graphs
set border 3
set tics out nomirror
set grid

set ylabel "ln ||bi*||"
set yrange [-1:5]
set xlabel "column index (i)"
set xtics 0,25

set terminal pngcairo size 1000,325


# ---------------------------------------------------------------------------------------------
# Appendix: 2 graphs. First is tw/phs0 after/before BKZ. 2. Is 5 graphs after BKZ

# Multiplot setting and legend
set output data_dir.label."_gsn_appendix.png"
# Determining Grid (multiplot), on a 4 graphs max / row basis
nc   = 2 #(nelt < 4)? nc = nelt : 4
nr   = 1 #(nelt) / nc + 1
set multiplot layout nr,nc rowsfirst
set key font ",10" samplen 2 reverse Left
set key box lt -1 lw 0.5 width 1


print "    Plot w/o BKE for twPHS/PHS for ".label
set size 0.45,1
set title "(a) w/o BKZ at iso FB"
plot tw    using 1:2 title "Tw-PHS (Raw)" with line dashtype 2 lc rgb 'red', \
     tw    using 1:3 title "Tw-PHS (BKZ)" with line dashtype 1 lc rgb 'tan1', \
     phs0  using 1:2 title "PHS (Raw)" with line dashtype 2 lc rgb 'royalblue', \
     phs0  using 1:3 title "PHS (BKZ)" with line dashtype 1 lc 2

unset ylabel
set size 0.55,1
set origin 0.45,0
set title "(b) after BKZ for all variants"
set key invert
plot phs   using 1:3 title "PHS" with line dashtype 1 lc 3, \
     opt   using 1:3 title "Opt-PHS" with line dashtype 1 lc rgb 'dark-violet', \
     phs0  using 1:3 title "PHS (twFB)" with line dashtype 1 lc 2, \
     opt0  using 1:3 title "Opt-PHS (twFB)" with line dashtype 1 lc rgb 'dark-pink', \
     tw    using 1:3 title "Tw-PHS" with line dashtype 1 lc rgb 'tan1'

exit status 0;

